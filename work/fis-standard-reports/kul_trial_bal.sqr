!
! Copyright 2005-2006 The Kuali Foundation.
! 
! $Source: /opt/cvs/kfs/work/fis-standard-reports/kul_trial_bal.sqr,v $
! 
! Licensed under the Educational Community License, Version 1.0 (the "License");
! you may not use this file except in compliance with the License.
! You may obtain a copy of the License at
! 
! http://www.opensource.org/licenses/ecl1.php
! 
! Unless required by applicable law or agreed to in writing, software
! distributed under the License is distributed on an "AS IS" BASIS,
! WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
! See the License for the specific language governing permissions and
! limitations under the License.
! DO NOT add comments before the blank line below, or they will disappear.

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! $Id: 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

#include "kul_error.h"
#include "kul_check.h"
#include "kul_common.h"
#include "kul_enter.h"


begin-heading 6

   #debug display 'entering HEADING'

   if #change_page_number = 1
      #debug display '- change_page_number is set'
      let #page_number = 1
      let #change_page_number = 0
   else
      let #page_number = #page_number + 1
   end-if

   #debug display 'current page number is ' NOLINE
   #debug display #page_number

   if #print_lf = 1
      #debug display '- printing linefeed'
      let $lb = chr(12) 
      print $lb (1,1)
      print ' .' (,2) 
   end-if

   ! add a couple spaces to the first line on each page.
   print '  ' (,1)
   print 'PAGE' (,3)
   print #page_number (,8,8) edit 99999999
   print 'TRIAL BALANCE REPORT' (,55)
   print $display_for_month (,120)


   print 'BEGINNING' (+1,37)
   print 'ENDING' (,80)

   print 'ACCOUNT' (+1,2)
   print 'ACCOUNT NAME' (,16)
   print 'FUND BALANCE' (,35)
   print 'RECEIPTS' (,51)
   print 'DISBURSEMENTS' (,63)
   print 'FUND BALANCE' (,77)
   print 'LIABILITIES' (,92)
   print 'ASSETS' (,108) 
   print 'CASH' (,123) 

   print '--------' (+1,1)
   print '------------------------' (,10)
   print '-------------' (,35)
   print '-------------' (,49)
   print '-------------' (,63)
   print '-------------' (,77)
   print '-------------' (,91)
   print '-------------' (,105) 
   print '-------------' (,119)

   let #line_count = 0
   let #print_lf = 0

end-heading



begin-program

   Do GetParms
   Do Main

end-program



begin-procedure GetParms

   Do SetConstantGlobal

   input $fiscal_year 'Enter Fiscal Year'
   input $fiscal_period 'Enter Fiscal Period' 

   let $display_univ_fscpd_cd = $fiscal_period

   let #calendar_yr = to_number( $fiscal_year )
   let #fiscal_period = to_number( $fiscal_period )
   if ( #fiscal_period <= 6 )
      let #calendar_yr = #calendar_yr - 1
   end-if
   let $calendar_yr = to_char( #calendar_yr )

   ! determine the number of days in February for the current year
   Do SetFebruaryEnd( #calendar_yr, $feb_day_last )

   evaluate $display_univ_fscpd_cd
      when = '01'
         let $display_for_month = 'JULY 31, ' || $calendar_yr
         break
      when = '02'
         let $display_for_month = 'AUG. 31, ' || $calendar_yr
         break
      when = '03'
         let $display_for_month = 'SEP. 30, ' || $calendar_yr
         break
      when = '04'
         let $display_for_month = 'OCT. 31, ' || $calendar_yr
         break
      when = '05'
         let $display_for_month = 'NOV. 30, ' || $calendar_yr
         break
      when = '06'
         let $display_for_month = 'DEC. 31, ' || $calendar_yr
         break
      when = '07'
         let $display_for_month = 'JAN. 31, ' || $calendar_yr
         break
      when = '08'
         let $display_for_month = 'FEB. ' || $feb_day_last || ', ' || $calendar_yr
         break
      when = '09'
         let $display_for_month = 'MAR. 31, ' || $calendar_yr
         break
      when = '10'
         let $display_for_month = 'APR. 30, ' || $calendar_yr
         break
      when = '11'
         let $display_for_month = 'MAY 31, ' || $calendar_yr
         break
      when = '12'
         let $display_for_month = 'JUNE 30, ' || $calendar_yr
         break
      when = '13'
         let $display_for_month = 'JUNE 30, ' || $calendar_yr
         break
      when-other
         let $display_for_month = ' '
   end-evaluate

end-procedure







begin-procedure Main

   let #page_number = 0
   let #campus_total_begbal = 0
   let #campus_total_recpts = 0
   let #campus_total_disbur = 0
   let #campus_total_cashbal = 0
   let #campus_total_liabs = 0
   let #campus_total_assets = 0
   let #campus_total_cash = 0
   let #fund_total_begbal = 0
   let #fund_total_recpts = 0
   let #fund_total_disbur = 0
   let #fund_total_cashbal = 0
   let #fund_total_liabs = 0
   let #fund_total_assets = 0
   let #fund_total_cash = 0
   let #total_begbal = 0
   let #total_recpts = 0
   let #total_disbur = 0
   let #total_cashbal = 0
   let #total_liabs = 0
   let #total_assets = 0
   let #total_cash = 0
   let #just_printed_campus = 0
   let #just_printed_fund = 0

BEGIN-SELECT
a.sub_fund_grp_cd       &sub_fund_grp_cd   ()                  ON-BREAK PRINT=NEVER LEVEL=1 AFTER=PrintFundSubtotals
a.sub_fund_grp_cd    as	a2                 ()                  ON-BREAK PRINT=NEVER LEVEL=1 BEFORE=PrintFundName
a.fin_coa_cd         	&fin_coa_cd        ()                  ON-BREAK PRINT=NEVER LEVEL=1 AFTER=PrintCampusSubtotals
a.sub_fund_grp_desc  	&sub_fund_grp_desc               
a.account_nbr                              (+1,1,8) 
a.account_nm            	           (,10,24)
a.begbal             	&begbal            (,35,14)  edit 99,999,999.99
a.recpts             	&recpts            (,49,14)  edit 99,999,999.99
a.disbur             	&disbur            (,63,14)  edit 99,999,999.99 
a.funbal               	&cashbal           (,77,14)  edit 99,999,999.99
a.liabil               	&liabs             (,91,14)  edit 99,999,999.99
a.assets               	&assets            (,105,14) edit 99,999,999.99
a.cashbal               &cash              (,119,14) edit 99,999,999.99

   let #begbal = &begbal
   let #recpts = &recpts
   let #disbur = &disbur
   let #cashbal = &cashbal
   let #liabs = &liabs
   let #assets = &assets
   let #cash = &cash
   let $old_fin_coa_cd = &fin_coa_cd
   let $old_sub_fund_grp_cd = &sub_fund_grp_cd 

   let #campus_total_begbal = #campus_total_begbal + #begbal
   let #campus_total_recpts = #campus_total_recpts + #recpts
   let #campus_total_disbur = #campus_total_disbur + #disbur
   let #campus_total_cashbal = #campus_total_cashbal + #cashbal
   let #campus_total_liabs  = #campus_total_liabs  + #liabs
   let #campus_total_assets = #campus_total_assets + #assets
   let #campus_total_cash   = #campus_total_cash   + #cash

   let #fund_total_begbal = #fund_total_begbal + #begbal
   let #fund_total_recpts = #fund_total_recpts + #recpts
   let #fund_total_disbur = #fund_total_disbur + #disbur
   let #fund_total_cashbal = #fund_total_cashbal + #cashbal
   let #fund_total_liabs  = #fund_total_liabs  + #liabs 
   let #fund_total_assets = #fund_total_assets + #assets
   let #fund_total_cash   = #fund_total_cash   + #cash  

   let #total_begbal = #total_begbal + #begbal
   let #total_recpts = #total_recpts + #recpts
   let #total_disbur = #total_disbur + #disbur
   let #total_cashbal = #total_cashbal + #cashbal
   let #total_liabs  = #total_liabs  + #liabs 
   let #total_assets = #total_assets + #assets
   let #total_cash   = #total_cash   + #cash  

   let #just_printed_campus = 0
   let #just_printed_fund = 0


FROM     kul_trial_bal_v a

ORDER BY a.sub_fndgrp_rsrt_cd,
         a.sub_fund_grp_cd,
         a.account_nbr

END-SELECT


   let $total_begbal = #total_begbal
   let $total_recpts = #total_recpts
   let $total_disbur = #total_disbur
   let $total_cashbal = #total_cashbal
   let $total_liabs  = #total_liabs 
   let $total_assets = #total_assets
   let $total_cash   = #total_cash  

   print 'GRAND TOTAL' (+1,2)
   print $total_begbal (,35,14)  edit 99,999,999.99
   print $total_recpts (,49,14)  edit 99,999,999.99
   print $total_disbur (,63,14)  edit 99,999,999.99
   print $total_cashbal (,77,14) edit 99,999,999.99
   print $total_liabs  (,91,14)  edit 99,999,999.99
   print $total_assets (,105,14) edit 99,999,999.99
   print $total_cash   (,119,14) edit 99,999,999.99


end-procedure


begin-procedure PrintFundName

   let $sub_fund_grp_desc = &sub_fund_grp_desc
   print $sub_fund_grp_desc (+1,2)

end-procedure



begin-procedure PrintFundSubtotals

   let $fund_total_begbal = #fund_total_begbal
   let $fund_total_recpts = #fund_total_recpts
   let $fund_total_disbur = #fund_total_disbur
   let $fund_total_cashbal = #fund_total_cashbal
   let $fund_total_liabs  = #fund_total_liabs 
   let $fund_total_assets = #fund_total_assets
   let $fund_total_cash   = #fund_total_cash  

   let $fund_line = $old_sub_fund_grp_cd || ' SUB FUND TOTAL'
   if #just_printed_campus = 1
      print $fund_line (+1,2)
   else
      print $fund_line (+2,2)
   end-if

   print $fund_total_begbal (+1,35,14) edit 99,999,999.99
   print $fund_total_recpts (,49,14)   edit 99,999,999.99
   print $fund_total_disbur (,63,14)   edit 99,999,999.99
   print $fund_total_cashbal (,77,14)  edit 99,999,999.99
   print $fund_total_liabs  (,91,14)   edit 99,999,999.99
   print $fund_total_assets (,105,14)  edit 99,999,999.99
   print $fund_total_cash   (,119,14)  edit 99,999,999.99
   print ' ' (+1,2)

   let #fund_total_begbal = 0
   let #fund_total_recpts = 0
   let #fund_total_disbur = 0
   let #fund_total_cashbal = 0
   let #fund_total_liabs  = 0
   let #fund_total_assets = 0
   let #fund_total_cash   = 0
   let #just_printed_fund = 1


end-procedure



begin-procedure PrintCampusSubtotals

   let $fin_coa_cd = &fin_coa_cd

   if $fin_coa_cd <> $old_fin_coa_cd
   let $campus_total_begbal = #campus_total_begbal
   let $campus_total_recpts = #campus_total_recpts
   let $campus_total_disbur = #campus_total_disbur
   let $campus_total_cashbal = #campus_total_cashbal
   let $campus_total_liabs  = #campus_total_liabs 
   let $campus_total_assets = #campus_total_assets
   let $campus_total_cash   = #campus_total_cash  

   let $chart_line = $old_fin_coa_cd || ' CHART TOTAL'
   if #just_printed_fund = 1
      print $chart_line (+1,2)
   else
      print $chart_line (+2,2)
   end-if

   print $campus_total_begbal (+1,35,14) edit 99,999,999.99
   print $campus_total_recpts (,49,14)   edit 99,999,999.99
   print $campus_total_disbur (,63,14)   edit 99,999,999.99
   print $campus_total_cashbal (,77,14)  edit 99,999,999.99
   print $campus_total_liabs  (,91,14)   edit 99,999,999.99
   print $campus_total_assets (,105,14)  edit 99,999,999.99
   print $campus_total_cash   (,119,14)  edit 99,999,999.99
   print ' ' (+1,2)

   let #campus_total_begbal = 0
   let #campus_total_recpts = 0
   let #campus_total_disbur = 0
   let #campus_total_cashbal = 0
   let #campus_total_liabs  = 0
   let #campus_total_assets = 0
   let #campus_total_cash   = 0 
   let #just_printed_campus = 1
 
   end-if


end-procedure


